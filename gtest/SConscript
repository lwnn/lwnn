from building import *
Import('gtenv')
Import('asenv')

asenv.Append(CPPDEFINES=['L_BLOB_NOT_BUILTIN'])
gtenv.Append(CPPDEFINES=['L_BLOB_NOT_BUILTIN'])

cwd = GetCurrentDir()
tgt = '%s/models/date.o'%(cwd)
src = '%s/gen_models.py'%(cwd)
if(IsPlatformWindows()):
    py = 'python'
else:
    py = 'python3'
cmd = 'cd %s && %s gen_models.py && date > %s'%(cwd, py, tgt)

MKObject(src, tgt, cmd)

objs = Glob('*.cpp')

if(not GetOption('android')):
    gtenv.Append(LIBS=['gtest','gtest_main','pthread','stdc++', 'dl', 'm'])
else:
    gtenv.Append(LIBS=['stdc++', 'dl', 'm'])
    NDK = gtenv['ANDROID_NDK']
    gtest = NDK+'/sources/third_party/googletest'
    gtenv.Append(CPPPATH=['%s/include'%(gtest), gtest])
    gobjs = Glob('%s/src/*.cc'%(gtest))
    SrcRemove(gobjs, ['gtest-all.cc','gtest_main.cc'])
    objs += gtenv.Library('gtest', gobjs)
    if('push' in COMMAND_LINE_TARGETS):
        if(IsPlatformWindows()):
            dllfix = '.dll'
            host = 'windows'
        else:
            dllfix = '.so'
            host = 'linux'
        for so in Glob('../build/android/gtest/models/*/*%s'%(dllfix)):
            src = str(so)
            tgt = '/data/local/tmp/lwnn/build/android/gtest/%s'%(src.replace('.dll','.so'))
            b = os.path.basename(tgt)
            if(b[:3] != 'lib'):
                tgt = tgt.replace(b, 'lib%s'%(b))
            AdbPush(src, tgt)
        for raw in Glob('models/*/golden/*.raw'):
            src = str(raw)
            tgt = src.split('gtest')[1]
            tgt = '/data/local/tmp/lwnn/gtest/%s'%(tgt)
            AdbPush(src, tgt)
        for bin in Glob('models/*/*.bin'):
            src = str(bin)
            tgt = src.split('gtest')[1]
            tgt = '/data/local/tmp/lwnn/gtest/%s'%(tgt)
            AdbPush(src, tgt)
        for cl in Glob('../nn/runtime/opencl/kernels/*.cl'):
            src = str(cl)
            tgt = '/data/local/tmp/lwnn/nn/runtime/opencl/kernels/%s'%(os.path.basename(src))
            AdbPush(src, tgt)
        for exe in Glob('../lwnn_gtest*'):
            src = str(exe)
            tgt = '/data/local/tmp/lwnn/%s'%(os.path.basename(src))
            AdbPush(src, tgt)
        cmd = 'adb push %s/toolchains/llvm/prebuilt/%s-x86_64/sysroot/usr/lib/aarch64-linux-android/libc++_shared.so /data/local/tmp/lwnn'%(NDK, host)
        RunCommand(cmd)
        exit()

objsM = Glob('models/*/*.c')
for obj in objsM:
    gtenv.SharedLibrary(obj)

Return('objs')
